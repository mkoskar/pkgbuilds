# Maintainer: Miroslav Koškár <http://mkoskar.com/>

_basename='vcvrack'

pkgname='vcvrack-git'
pkgver=0.5.1.r64.gca5d43e
pkgrel=1
pkgdesc='Open-source virtual modular synthesizer'
url='https://vcvrack.com/'
license=(BSD)
arch=(i686 x86_64)
depends=(glew jansson speexdsp libsamplerate openssl curl libzip rtmidi jack libpulse gtk2)
conflicts=(vcvrack)
makedepends=(git cmake)

source=(
    "$_basename::git+https://github.com/VCVRack/Rack.git#branch=master"
    git+https://github.com/AndrewBelt/osdialog.git
    git+https://github.com/AndrewBelt/oui-blendish.git
    git+https://github.com/mackron/dr_libs.git
    git+https://github.com/memononen/nanosvg.git
    git+https://github.com/memononen/nanovg.git
    git+https://github.com/glfw/glfw.git
    git+https://github.com/thestk/rtaudio.git
    vcvrack.sh
)
sha256sums=(
    SKIP
    SKIP
    SKIP
    SKIP
    SKIP
    SKIP
    SKIP
    SKIP
    535e3f427e92e793e23c153e9dff3f3c85f8d29d42bb0459b12f733bb6c7f726
)

pkgver() {
    cd "$_basename"
    git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "$_basename"
    git submodule init
    git config submodule.ext/dr_libs.url "$srcdir/dr_libs"
    git config submodule.ext/nanosvg.url "$srcdir/nanosvg"
    git config submodule.ext/nanovg.url "$srcdir/nanovg"
    git config submodule.ext/osdialog.url "$srcdir/osdialog"
    git config submodule.ext/oui-blendish.url "$srcdir/oui-blendish"
    git config submodule.dep/glfw.url "$srcdir/glfw"
    git config submodule.dep/rtaudio.url "$srcdir/rtaudio"
    git submodule update
}

build() {
    cd "$_basename"

    make -C dep lib/libglfw.so lib/librtaudio.so \
        RTAUDIO_ALL_APIS=1 \
        RTAUDIO_FLAGS='-DAUDIO_LINUX_ALSA=ON -DAUDIO_LINUX_PULSE=ON -DAUDIO_UNIX_JACK=ON'

    local FLAGS
    FLAGS=$(pkg-config --cflags glew jansson samplerate openssl \
        libcurl libzip rtmidi)
    FLAGS="$FLAGS" VERSION="$pkgver" make
}

package() {
    cd "$_basename"
    install -D -m755 "$srcdir/vcvrack.sh" "$pkgdir/usr/bin/vcvrack"
    install -D -m644 -t "$pkgdir/usr/share/licenses/$_basename" LICENSE*
    install -D -m755 -t "$pkgdir/opt/$_basename" Rack
    install -D -m755 -t "$pkgdir/opt/$_basename/lib" dep/lib/lib*
    install -d "$pkgdir/opt/$_basename/plugins"
    cp -dr --preserve=mode -t "$pkgdir/opt/$_basename" dep/include
    cp -dr --preserve=mode -t "$pkgdir/opt/$_basename" res
}
